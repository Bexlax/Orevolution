plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id("org.spongepowered.mixin") version "0.7-SNAPSHOT"
    id("com.diffplug.spotless") version "7.0.4"
    id("org.parchmentmc.librarian.forgegradle") version "1.+"
    id("org.sonarqube") version "6.2.0.5505"
    id("com.modrinth.minotaur") version "2.+"
    id("net.darkhax.curseforgegradle") version "1.1.15"
}

version = mod_version
group = mod_group_id

base {
    archivesName = "$mod_id-$mod_version-forge"
}

mixin {
    add sourceSets.main, 'orevolution.refmap.json'
    config 'orevolution.mixins.json'

    dumpTargetOnFailure = true
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: mapping_channel, version: mapping_version

    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')

            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main {
    resources.srcDir("src/generated/resources")
}

repositories {
    maven {
        url = uri("https://api.modrinth.com/maven")
        content {
            includeGroup("maven.modrinth")
        }
    }

    maven {
        url = uri("https://registry.somethingcatchy.net/repository/maven-releases/")
        content {
            includeGroup("com.possible-triangle")
        }
    }

    maven { url = "https://maven.terraformersmc.com/" }
}

jarJar.enable()

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    implementation fg.deobf("maven.modrinth:blueprint:${blueprint_ver}")
    implementation fg.deobf("maven.modrinth:oreganized:${oreganized_ver}")
    implementation fg.deobf("maven.modrinth:caverns-and-chasms:${cac_ver}")
    implementation fg.deobf("maven.modrinth:farmers-delight:${farmersdelight_ver}")

    // oreganized extras
    implementation fg.deobf("com.possible-triangle:multikulti-core-forge:1.20.1-1.0.4")
    implementation fg.deobf("com.possible-triangle:multikulti-datagen-forge:1.20.1-1.0.4")
    implementation fg.deobf("com.possible-triangle:multikulti-datagen-forge-fix:1.20.1-1.0.4")

    // runtimeOnly fg.deobf("maven.modrinth:shield-expansion:${shieldex_ver}")
    runtimeOnly fg.deobf("maven.modrinth:embeddium:${embeddium_ver}")
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy 'reobfJar'
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
